# Задание 1
##### Приложите скриншот входящих правил "Группы безопасности" в ЛК Yandex Cloud или скриншот отказа в предоставлении доступа к preview версии.
<img width="610" alt="image" src="https://github.com/nehardcore/terraform/assets/97674120/87297d04-3528-4a2f-84d4-98ec1da68ac4">
  
# Задание 2
##### 1. Создайте файл count-vm.tf. Опишите в нем создание двух одинаковых ВМ web-1 и web-2(не web-0 и web-1!), с минимальными параметрами, используя мета-аргумент count loop. Назначьте ВМ созданную в 1-м задании группу безопасности.

```terraform
resource "yandex_compute_instance" "web" {
    depends_on = [ yandex_vpc_subnet.develop ]
    count = 2
    name = "web-${count.index + 1}"
    zone = var.default_zone

    resources {
        cores           = var.vm_resources.cores
        memory          = var.vm_resources.memory
        core_fraction   = var.vm_resources.core_fraction
    }
    
    boot_disk {
        initialize_params {
            image_id = data.yandex_compute_image.ubuntu.image_id
        }
     }
    scheduling_policy {
        preemptible = true
    }
    network_interface {
        subnet_id = yandex_vpc_subnet.develop.id
        security_group_ids = [yandex_vpc_security_group.example.id]

    }
    metadata = {
      ssh_key = var.ssh_key
    }
}
```

##### 2.
```terraform
variable "vm_config" {
  type = list(object(
    {
      vm_name   = string,
      cpu       = number,
      ram       = number,
      disk      = number 
  }))
  default = [{
    cpu     = 2
    disk    = 15
    ram     = 2
    vm_name = "main"
  },
  {
    cpu     = 2
    disk    = 10
    ram     = 1
    vm_name = "replica"    
  }]
}

resource "yandex_compute_instance" "db" {
    depends_on = [ yandex_compute_instance.web ]
    for_each = { for k in var.vm_config : k.vm_name => k }
    name = each.value.vm_name
    zone = var.default_zone

    resources {
        cores           = each.value.cpu
        memory          = each.value.ram
        core_fraction   = var.vm_resources.core_fraction
    }

    boot_disk {
        initialize_params {
            image_id = data.yandex_compute_image.ubuntu.image_id
            size = each.value.disk
        }
     }
    scheduling_policy {
        preemptible = true
    }
    network_interface {
        subnet_id = yandex_vpc_subnet.develop.id
        security_group_ids = [yandex_vpc_security_group.example.id]

    }
    metadata = {
      ssh_key = data.local_file.public_ssh_key.content
    }
}

provider "local" {
}

data "local_file" "public_ssh_key" {
  filename = "/users/cch/.ssh/id_ed25519.pub"
}
```

<img width="1297" alt="image" src="https://github.com/nehardcore/terraform/assets/97674120/c2ef3a45-d8c2-4f21-aee8-b21eb6954df9">

##### 3.
```terraform
resource "yandex_compute_disk" "storage_disks" {
    count   = 3
    size    = 1
}

resource "yandex_compute_instance" "storage" {
    depends_on = [ yandex_compute_disk.storage_disks ]
    name = "storage"
    zone = var.default_zone

    resources {
        cores           = var.vm_resources.cores
        memory          = var.vm_resources.memory
        core_fraction   = var.vm_resources.core_fraction
    }
    
    allow_stopping_for_update = true

    dynamic secondary_disk{
        for_each = yandex_compute_disk.storage_disks
        content {
          disk_id = secondary_disk.value.id
        }
    }

    boot_disk {
        initialize_params {
            image_id = data.yandex_compute_image.ubuntu.image_id
        }
     }
    scheduling_policy {
        preemptible = true
    }
    network_interface {
        subnet_id = yandex_vpc_subnet.develop.id
        security_group_ids = [yandex_vpc_security_group.example.id]

    }
    metadata = {
      ssh_key = var.ssh_key
    }
}
```
<img width="1303" alt="image" src="https://github.com/nehardcore/terraform/assets/97674120/95d3a89a-b247-4dc1-b5f7-4e18d2813def">

